* Create a format to display colors ranging from Blue (cold) to Red (hot) ;
data _null_ ;
  call execute('proc format fmtlib ; value pct_') ;
  max=1;
  maxloop=255 ;
  do i=1 to maxloop ;
    color='cx'||put(i/maxloop*255,hex2.)||'00'||put((maxloop-i)/maxloop*255,hex2.) ;
    from=((i-1)/maxloop)*max ;
    to=(i/maxloop)*max ;
    call execute(put(from,best.)||'-'||put(to,best.)||'='||quote(color)) ;
  end ;
  call execute('.="light gray" other="cxd0d0d0" ; run ;') ;
run ;
* get the maximum value of air ;
proc sql ;
  select max(air),min(air) into :max,:min from sashelp.air ;
%let range=%sysevalf(&max-&min) ;
* express values of air as a percentage of the maximum ;
data air ;
  set sashelp.air ;
  year=year(date) ;
  month=month(date) ;
 * percentage is the level above the minimum ;
  pct=(air-&min)/&range ;
run ;
* tabulate the results indicating maximum as red, minimum as blue ;
ods html file='test.html' ;
title 'Air Quality over the years' ;
footnote 'Blue (cold) is best, Red (hot) is worst' ;
proc tabulate data=air style={background=pct_.} ;
  class year month ;
  var pct ;
  label pct='Air Quality Percent of worst month' ;
  table sum=''*pct*f=percent.,year='Year',month='Month of Year' ;
run ;
ods html close ;
